buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
    }
}
apply plugin: "net.minecraftforge.gradle.forge"

def mod_file = getModFile()
version = getVersion("VERSION", mod_file)
def llibrary_version = getVersion("LLIBRARY_VERSION", mod_file)
def top_version = getVersion("TOP_VERSION", mod_file)
def waila_version = getVersion("WAILA_VERSION", mod_file)
group = "net.ilexiconn"
archivesBaseName = "lawnmower"
sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    version = "1.10.2-12.18.1.2011"
    runDir = "minecraft"
    mappings = "snapshot_20160518"
}

repositories {
	mavenCentral()
	maven {
		name = "ilexiconn"
		url = "http://maven.ilexiconn.net"
	}
    maven {
        name = "tterrag"
        url = "http://maven.tterrag.com/"
    }
    maven {
        name = "mobius"
        url = "http://mobiusstrip.eu/maven"
    }
}

dependencies {
    compile "net.ilexiconn:llibrary:$llibrary_version-1.10.2:dev"
    deobfCompile "mcjty.theoneprobe:TheOneProbe:1.10-$top_version"
    compile "mcp.mobius.waila:Waila:" + waila_version + "_1.9.4"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "llibrary_version", llibrary_version

    from (sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version, "llibrary_version": llibrary_version
    }
        
    from (sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    exclude ("llibrary.mappings")
    manifest {
        attributes "FMLAT": "lawnmower_at.cfg"
        attributes "FMLCorePlugin": "net.ilexiconn.lawnmower.server.asm.plugin.LawnmowerPlugin"
        attributes "FMLCorePluginContainsFMLMod": "true"
    }
}

String getModFile() {
    String path = "";
    FileTree tree = fileTree(dir: 'src/main/java')
    tree.include '**/*.java'
    tree.visit {element ->
        if (element.file.isFile()) {
            element.file.eachLine { String s ->
                s = s.trim();
                if (s.startsWith("@Mod")) {
                    path = "src/main/java/$element.relativePath"
                }
            }
        }
    }
    return path;
}

String getVersion(String type, String mod_file) {
    String major = "0";
    String revision = "0";
    String patch = "0";
    String prefix = "public static final String $type = \"";
    File file = file(mod_file)
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            String[] pts = s.split("\\.");

            major = pts[0];
            revision = pts[1];
            patch = pts[2];
        }
    }
    return "$major.$revision.$patch";
}